// Notice that we support multiple accounts, in this case we're using the first account.
// If you want to use a different account, you can change the index and add multiple versions of this tool.

@wire(search-emails-inner {
  = input
  input.query | ExpectString = query
  input.account-index | If(IsInt ExpectInt {1}) | Google.Auth = google-token
  
  {
    "Authorization": (["Bearer " google-token] | String.Join)
    "Accept": "application/json"
  } = headers
  
  "" >= emails
  
  {
    maxResults: "15" // limit to 15 results
    q: query
    orderBy: "date" // order by date
  } | Http.Get("https://gmail.googleapis.com/gmail/v1/users/me/messages" Headers: headers)
  Await(FromJson) | ExpectTable | Take("messages")
  When(IsSeq {
    ExpectSeq
    ForEach({
      $0.id | ExpectString = message-id
      $0.threadId | ExpectString = thread-id
      
      "" >= subject
      "" >= from
      "" >= date
      ["https://gmail.googleapis.com/gmail/v1/users/me/messages/" message-id] | String.Join = url
      {format: "metadata"} | Http.Get(url Headers: headers)
      Await(FromJson) = message
      message.snippet | ExpectString = snippet
      message.payload | ExpectTable | Take("headers") | ExpectSeq
      ForEach({
        $0.name | ExpectString = header-name
        $0.value | ExpectString = header-value
        
        header-name | When(Is("Subject") {
          header-value > subject
        })
        header-name | When(Is("From") {
          header-value > from
        })
        header-name | When(Is("Date") {
          header-value > date
        })
      })
      
      [
        "ID: " message-id
        "\nFrom: " from
        "\nSubject: " subject
        "\nDate: " date
        "\nSnippet: " snippet
        "\n\n"
      ] | String.Join
      AppendTo(emails)
    })
  })
  
  emails
} Pure: true)

@wire(search-emails {
  Do(search-emails-inner)
})

{
  definition: {
    name: "search_emails"
    description: "Search for emails in the user's inbox."
    parameters: {
      type: "object"
      properties: {
        query: {
          type: "string"
          description: "The query in google gmail search syntax to search for. e.g. 'is:unread label:inbox newer_than:2d OR (is:important label:inbox newer_than:2d)'"
        }
        account-index: {
          type: "integer"
          description: "The index of the gmail account to use (Users can have multiple gmail accounts setup). Default is 1 (first account), 0 and less is invalid."
        }
      }
      required: ["query"]
    }
  }
  
  use: search-emails
}

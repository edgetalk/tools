@define(headers {
  "Content-Type": "application/json"
  "User-Agent": "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/120.0.0.0 Safari/537.36"
  "Accept-Language": "en-US,en;q=0.9"
})

@wire(youtube-captions {
  = input
  
  input.video_id | ExpectString = video-id
  input.language_code | ExpectString = language-code
  
  "" >= output
  
  {
    "context": {
      "client": {
        "hl": "en"
        "clientName": "WEB"
        "clientVersion": "2.20240101.00.00"
        "clientFormFactor": "UNKNOWN_FORM_FACTOR"
        "clientScreen": "WATCH"
      }
      "user": {
        "lockedSafetyMode": false
      }
      "request": {
        "useSsl": true
        "internalExperimentFlags": []
        "consistencyTokenJars": []
      }
    }
    "videoId": video-id
    "playbackContext": {
      "contentPlaybackContext": {
        "vis": 0
        "splay": false
        "autoCaptionsDefaultOn": false
        "autonavState": "STATE_NONE"
        "html5Preference": "HTML5_PREF_WANTS"
        "lactMilliseconds": "-1"
      }
    }
    "racyCheckOk": false
    "contentCheckOk": false
  } | ToJson | Http.Post("https://www.youtube.com/youtubei/v1/player?key=AIzaSyAO_FJ2SlqU8Q4STEHLGCilw_Y9_11qcW8" Headers: @headers Timeout: 30)
  Await(FromJson) = data
  
  data.videoDetails = video-details
  [
    "Title: " video-details.title "\n\n"
    "Author: " video-details.author "\n\n"
    "Description:\n" video-details.shortDescription "\n\n"
  ] | String.Format | AppendTo(output)
  
  data.captions = captions
  captions.playerCaptionsTracklistRenderer = renderer
  renderer.captionTracks | ExpectSeq | ForEach({
    = caption
    caption.baseUrl | ExpectString = base-url
    caption.languageCode | ExpectString | When(Is(language-code) {
      none | Http.Get(base-url Headers: @headers Timeout: 30) | AppendTo(output)
      Return // exit the ForEach
    })
  })
  
  output
})

{
  definition: {
    name: "youtube_captions"
    description: "Get captions for a YouTube video in a specific language. Use this tool when the user asks you to check a YouTube video."
    parameters: {
      type: "object"
      properties: {
        video_id: {
          type: "string"
          description: "The ID of the YouTube video to get captions for."
        }
        language_code: {
          type: "string"
          description: "The language code of the captions to get. For example, 'en' for English or 'es' for Spanish."
        }
      }
      required: ["video_id" "language_code"]
    }
  }
  
  use: youtube-captions
}

// // Test code
// {
//   "video_id": "JswxPY1f0lw"
//   "language_code": "en"
// } | Do(youtube-captions) | Log("captions")
